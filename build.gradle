plugins {
    id 'architectury-plugin' version "${architect_plugin_version}" apply false
    id 'dev.architectury.loom' version "${architectury_loom_version}" apply false
    id 'com.github.johnrengelman.shadow' version "${shadow_plugin_version}" apply false
    id 'org.ajoberstar.grgit' version "${grgit_plugin_version}"
}

apply from: 'scripts/helper.gradle'
apply from: 'scripts/meta.gradle'

def ENV = System.getenv()

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'architectury-plugin'
    apply plugin: 'dev.architectury.loom'

    group = 'games.moegirl.sinocraft'
    version = project.sinoseries_version

    architectury {
        minecraft = rootProject.minecraft_version
    }

    loom {
        silentMojangMappingsLicense()
        setEnableTransitiveAccessWideners true
        setEnableModProvidedJavadoc true
    }

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        maven {
            name = 'NeoForge'
            url = 'https://maven.neoforged.net/releases/'
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }

            filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_version}@zip")
        }
    }

    java {
        withSourcesJar()
    }

    sourcesJar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    sourceSets.main.resources {
        srcDir 'src/main/resources'
        srcDir 'src/generated/resources'
    }

    processResources {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        def copyDetails = (copy) -> {
            expand(project.properties)
        }

        filesMatching('*/**/mods.toml', copyDetails)
        filesMatching('fabric.mod.json', copyDetails)
        filesMatching('pack.mcmeta', copyDetails)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 17
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                name = 'YuluoMaven'

                def releaseUrl = 'https://maven.yuluo.dev/repository/maven-releases/'
                def snapshotUrl = 'https://maven.yuluo.dev/repository/maven-snapshots/'
                url = ENV.SINOSERIES_RELEASE ? releaseUrl : snapshotUrl

                credentials {
                    username System.getenv('SINOSERIES_MAVEN_USER')
                    password System.getenv('SINOSERIES_MAVEN_PASS')
                }
            }
        }
    }

    // Fixme: qyl27: Due to an unknown issue that forge-like projects can't clean the output resources dir.
    compileJava.dependsOn(clean)
}

subMod.each { Project mod ->
    configure(mod) {
        version = project.sinoseries_version

        architectury {
            common(((String) mod.mod_platforms).split(','))
        }

        loom {
            var awFile = new File(mod.projectDir, "src/main/resources/${mod.modid}.accesswidener")
            if (awFile.exists()) {
                accessWidenerPath = awFile
            }

            var commonFile = new File(mod.projectDir, 'src/main/resources/architectury.common.json')
            if (commonFile.exists()) {
                interfaceInjection {
                    enableDependencyInterfaceInjection = true
                }
            }
        }

        dependencies {
            if (!mod.name.startsWith('sinocore')) {
                api project(path: ':sinocore', configuration: 'namedElements')
            }

            compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
        }
    }

    configure(mod.subprojects) { Project sub ->
        apply plugin: 'com.github.johnrengelman.shadow'

        version = project.sinoseries_version

        configurations {
            common
            shadowCommon
            compileClasspath.extendsFrom common
            runtimeClasspath.extendsFrom common
            developmentForge.extendsFrom common
            developmentFabric.extendsFrom common
            developmentNeoForge.extendsFrom common
        }

        shadowJar {
            configurations = [project.configurations.shadowCommon]
            archiveClassifier = 'dev-shadow'
        }

        remapJar {
            input.set shadowJar.archiveFile
            dependsOn shadowJar
        }

        sourcesJar {
            var commonSources = mod.sourcesJar
            dependsOn commonSources
            from commonSources.archiveFile.map { zipTree(it) }
        }

        components.java {
            withVariantsFromConfiguration(sub.configurations.shadowRuntimeElements) {
                skip()
            }
        }

        dependencies {
            if (!mod.name.startsWith('sinocore')) {
                api project(path: ':sinocore', configuration: 'namedElements')
            }

            compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
        }

        if (mod.name != 'sinotest') {
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        version = helpers.getMavenVersion(sub)
                        from components.java
                    }
                }
            }
        }
    }

    configure(helpers.getSubForgeMod(mod)) { Project forgeMod ->
        apply plugin: 'com.github.johnrengelman.shadow'

        architectury {
            platformSetupLoomIde()
            forge()
        }

        loom {
            forge {
                if (loom.accessWidenerPath.isPresent()) {
                    convertAccessWideners = true
                    extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
                }

                if (forgeMod.file("/src/main/resources/${mod.modid}.mixins.json").exists()) {
                    mixinConfig "${mod.modid}.mixins.json"
                }

                if (mod.file("/src/main/resources/${mod.modid}-common.mixins.json").exists()) {
                    mixinConfig "${mod.modid}-common.mixins.json"
                }
            }

            runConfigs.configureEach { config ->
                ideConfigGenerated = false
            }

            runs {
                runData {
                    data()

                    ideConfigGenerated = true
                    configName = "Data Generator"

                    programArgs '--all', '--mod', "${mod.modid}",
                            '--output', helpers.getProjectGeneratedResourcesDir(mod).absolutePath

                    subMod.each { sub ->
                        helpers.getProjectResourcesDirs(sub).each { dir ->
                            programArgs '--existing', dir.absolutePath
                        }
                    }
                }
            }
        }

        sourceSets.main.resources {
            srcDir(new File(mod.projectDir, 'src/main/resources'))
            srcDir(new File(mod.projectDir, 'src/generated/resources'))
        }

        dependencies {
            forge "net.minecraftforge:forge:${mod.minecraft_version}-${forgeMod.forge_version}"

            common(project(path: mod.path, configuration: 'namedElements')) { transitive = false }
            shadowCommon(project(path: mod.path, configuration: 'transformProductionForge')) { transitive = false }
        }

        shadowJar {
            exclude 'fabric.mod.json'
            exclude 'architectury.common.json'

            configurations = [project.configurations.shadowCommon]
            archiveClassifier = 'dev-shadow'
        }
    }

    configure(helpers.getSubFabricMod(mod)) { Project fabricMod ->
        apply plugin: 'com.github.johnrengelman.shadow'

        architectury {
            platformSetupLoomIde()
            fabric()
        }

        loom {
            accessWidenerPath = mod.loom.accessWidenerPath

            runConfigs.configureEach { config ->
                ideConfigGenerated = false
            }
        }

        dependencies {
            modImplementation "net.fabricmc:fabric-loader:${fabricMod.fabric_loader_version}"
            modApi "net.fabricmc.fabric-api:fabric-api:${fabricMod.fabric_api_version}"

            common(project(path: mod.path, configuration: 'namedElements')) { transitive = false }
            shadowCommon(project(path: mod.path, configuration: 'transformProductionFabric')) { transitive = false }
        }

        shadowJar {
            exclude 'architectury.common.json'

            configurations = [fabricMod.configurations.shadowCommon]
            archiveClassifier = 'dev-shadow'
        }
    }

    configure(helpers.getSubNeoForgeMod(mod)) { Project neoForgeMod ->
        apply plugin: 'com.github.johnrengelman.shadow'

        architectury {
            platformSetupLoomIde()
            neoForge()
        }

        loom {
            neoForge {
                if (loom.accessWidenerPath.isPresent()) {
//                    convertAccessWideners = true
//                    accessTransformer loom.accessWidenerPath.get().asFile.name
                }
            }

            runConfigs.configureEach { config ->
                ideConfigGenerated = false
            }
        }

        sourceSets.main.resources {
            srcDir(new File(mod.projectDir, 'src/main/resources'))
            srcDir(new File(mod.projectDir, 'src/generated/resources'))
        }

        dependencies {
            neoForge "net.neoforged:neoforge:${project.neoforge_version}"

            common(project(path: mod.path, configuration: 'namedElements')) { transitive = false }
            shadowCommon(project(path: mod.path, configuration: 'transformProductionNeoForge')) { transitive = false }
        }

        shadowJar {
            exclude 'fabric.mod.json'
            exclude 'architectury.common.json'

            configurations = [project.configurations.shadowCommon]
            archiveClassifier = 'dev-shadow'
        }
    }
}

loom {
    runConfigs.configureEach { config ->
        ideConfigGenerated = false
    }
}

logger.lifecycle("Building SinoSeries Ver: ${helpers.getMavenVersion(project)}")
helpers.reGenRunConfig()
